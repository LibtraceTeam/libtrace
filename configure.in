# Process this file with autoconf to produce a configure script.

# Now you only need to update the version number in two places - below,
# and in the README

AC_INIT([libtrace],[4.0.24],[shane@alcock.co.nz],[libtrace])

LIBTRACE_MAJOR=4
LIBTRACE_MID=0
LIBTRACE_MINOR=24

# OpenSolaris hides libraries like libncurses in /usr/gnu/lib, which is not
# searched by default - add it to LDFLAGS so we at least have a chance of 
# finding it
if test -d "/usr/gnu/lib"; then
	LDFLAGS+=" -L/usr/gnu/lib"
fi

AC_CONFIG_MACRO_DIR([m4])
AC_CONFIG_SRCDIR(lib/trace.c)
AM_INIT_AUTOMAKE([subdir-objects])

# Make sure we use the relatively silent automake output
m4_ifdef([AM_SILENT_RULES],[AM_SILENT_RULES([yes])])

# Define our libtrace version number externally so we can use it in the source
# as well, if needed.
AC_DEFINE([LIBTRACE_MAJOR],${LIBTRACE_MAJOR},[libtrace major version])
AC_DEFINE([LIBTRACE_MID],${LIBTRACE_MID},[libtrace mid version])
AC_DEFINE([LIBTRACE_MINOR],${LIBTRACE_MINOR},[libtrace minor version])

# These are all the files we want to be built for us by configure
AC_CONFIG_FILES([Makefile lib/Makefile docs/Makefile libpacketdump/Makefile
	tools/Makefile tools/traceanon/Makefile tools/tracepktdump/Makefile 
	tools/tracemerge/Makefile tools/tracereport/Makefile
	tools/tracertstats/Makefile tools/tracesplit/Makefile
	tools/tracestats/Makefile tools/tracetop/Makefile
	tools/tracereplay/Makefile tools/tracediff/Makefile
	tools/traceends/Makefile tools/tracemcast/Makefile
	examples/Makefile examples/skeleton/Makefile examples/rate/Makefile
	examples/stats/Makefile examples/tutorial/Makefile examples/parallel/Makefile
	docs/libtrace.doxygen 
	lib/libtrace.h
	])


# Function that checks if the C++ compiler actually works - there's a bit of
# oversight in autoconf that will set the C++ compiler to g++ if no compiler
# is found, even if g++ is not present! So we need an extra test to make sure
# that the compiler works :(

AC_DEFUN([rw_PROG_CXX_WORKS],
[AC_REQUIRE([AC_PROG_CXX])dnl
AC_CACHE_CHECK([whether the C++ compiler works],
	[rw_cv_prog_cxx_works],
	[AC_LANG_PUSH([C++])
	AC_LINK_IFELSE([AC_LANG_PROGRAM([], [])],
		[rw_cv_prog_cxx_works=yes],
		[rw_cv_prog_cxx_works=no])
	AC_LANG_POP([C++])])
])

# Put all our automake definitions in config.h
AC_CONFIG_HEADERS([config.h])

# Checks for C and C++ compilers
AC_PROG_CC
AC_PROG_CXX
rw_PROG_CXX_WORKS

if test "$rw_cv_prog_cxx_works" = "no"; then
	AC_MSG_ERROR("Failed to find working C++ compiler")
fi

# Checking for 'install'
LT_INIT

# Checking for bison and flex
AC_CHECK_PROGS(YACC, 'bison -y' byacc yacc) 
AC_PROG_LEX(noyywrap)

PKG_PROG_PKG_CONFIG

# All our source files for function replacements are in lib/
AC_CONFIG_LIBOBJ_DIR(lib)

# Check for various "replacement" functions
AC_FUNC_MALLOC
AC_FUNC_REALLOC

# Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE
AC_C_BIGENDIAN
AC_TYPE_SIZE_T
AC_CHECK_HEADERS_ONCE([sys/time.h])

AC_SYS_LARGEFILE

LIBPKTDUMP_LIBS="-ltrace "
ADD_LDFLAGS="$ADD_LDFLAGS -L\$(abs_top_srcdir)/lib"
LIBTRACE_LIBS=""
TOOLS_LIBS=""

CFLAGS="$CFLAGS  -Wall -Wmissing-prototypes -Wextra -DLT_BUILDING_DLL=1"
CXXFLAGS="$CXXFLAGS  -Wall -DLT_BUILDING_DLL=1"
#CFLAGS="$CFLAGS -fsanitize=address -Wall -Wmissing-prototypes -Wextra -DLT_BUILDING_DLL=1"
#CXXFLAGS="$CXXFLAGS -fsanitize=address -Wall -DLT_BUILDING_DLL=1"

AC_ARG_ENABLE(address-san, AS_HELP_STRING(--enable-address-san, Enable address and memory sanitisation),
[
  if test "x$enableval" = xyes ; then
    CFLAGS+=" -fsanitize=undefined,leak,address -fno-omit-frame-pointer -ggdb3"
    CXXFLAGS+=" -fsanitize=undefined,leak,address -fno-omit-frame-pointer -ggdb3"
    LDFLAGS+=" -fsanitize=undefined,leak,address -fno-omit-frame-pointer -ggdb3"

    AC_MSG_NOTICE([Compiling with -fsanitize=undefined,leak,address])
    AC_MSG_NOTICE([ - Build tests using the command: make address-san])
    AC_MSG_NOTICE([ - Programs must also be compiled with -fsanitize=undefined,leak,address])
  fi
 ], [])

AC_ARG_ENABLE(thread-san, AS_HELP_STRING(--enable-thread-san, Enable thread sanitisation),
[
  if test "x$enableval" = xyes ; then
    if test "x$enable_address_san" = xyes ; then
        AC_MSG_ERROR("--enable-address-san and --enable-thread-san cannot be used together")
    fi
    CFLAGS+=" -fsanitize=thread -fno-omit-frame-pointer -ggdb3"
    CXXFLAGS+=" -fsanitize=thread -fno-omit-frame-pointer -ggdb3"
    LDFLAGS+=" -fsanitize=thread -fno-omit-frame-pointer -ggdb3"

    AC_MSG_NOTICE([Compiling with -fsanitize=thread])
    AC_MSG_NOTICE([ - Build tests using the command: make thread-san])
    AC_MSG_NOTICE([ - Programs must also be compiled with -fsanitize=thread])
  fi
 ], [])

# Check for -fvisibility
gl_VISIBILITY

gcc_PACKED
gcc_ALIGNED
gcc_DEPRECATED
gcc_UNUSED
gcc_PURE
gcc_FORMAT

# Check for gcc style TLS (__thread)
gcc_TLS
	
# Check for libtool
LT_INIT

# Checks for library functions.
AC_PROG_GCC_TRADITIONAL

# Fail if any of these functions are missing
AC_CHECK_DECLS([strdup, strlcpy, strcasecmp, strncasecmp, snprintf, vsnprintf, strndup, posix_memalign])
AC_CHECK_DECLS([socket, recvmmsg], [], [], [[#define _GNU_SOURCE 1
#include <sys/socket.h>]])
AC_CHECK_SIZEOF([long int])


# Checks for header files.
AC_CHECK_HEADERS(pcap.h pcap-bpf.h sys/limits.h stddef.h inttypes.h limits.h net/ethernet.h sys/prctl.h)


# OpenSolaris puts ncurses.h in /usr/include/ncurses rather than /usr/include,
# so check for that
AC_CHECK_HEADERS(ncurses.h,,[missing_ncurses=true])

if test "x$missing_ncurses" = xtrue; then
	if test -f "/usr/include/ncurses/ncurses.h"; then
		AC_DEFINE(HAVE_NCURSES_NCURSES_H,1,[Has nested ncurses header])
	fi
fi


# Check for the presence of various networking headers and define appropriate
# macros
AC_CHECK_HEADERS(netinet/in.h)
AC_CHECK_HEADERS(netpacket/packet.h,[
	libtrace_netpacket_packet_h=true
	AC_DEFINE(HAVE_NETPACKET_PACKET_H,1,[has net])
])
AC_CHECK_HEADER(net/if.h,
	AC_DEFINE(HAVE_NET_IF_H,1,[has net/if.h header]),,[
 #include <sys/types.h>
 #include <sys/socket.h>
])

# Check for linux/ethtool.h
AC_CHECK_HEADER(linux/ethtool.h,
	found_ethtool=true,
	found_ethtool=false,
)

AC_CHECK_HEADER(netinet/ether.h,
	AC_DEFINE(HAVE_NETINET_ETHER_H,1,[has netinet/ether.h header]),,[
 #include <sys/types.h>
 #include <sys/socket.h>
 #include <netinet/in.h>
 #include <net/if.h>
])

AC_CHECK_HEADER(netinet/if_ether.h,
	AC_DEFINE(HAVE_NETINET_IF_ETHER_H,1,[has netinet/if_ether.h]),,[
 #include <sys/types.h>
 #include <sys/socket.h>
 #include <netinet/in.h>
 #include <net/if.h>
])

AC_CHECK_HEADER(net/if_arp.h,
	AC_DEFINE(HAVE_NET_IF_ARP_H,1,[has net/if_arp.h]),,[
 #include <sys/types.h>
 #include <sys/socket.h>
 #include <netinet/in.h>
 #include <net/if.h>
])

# Check for sdl_len in sockaddr_dl - sockaddr_dl is used on BSD systems
if test "$libtrace_netpacket_packet_h" != "true"; then
	AC_CHECK_MEMBER([struct sockaddr_dl.sdl_len], 
		AC_DEFINE(HAVE_SDL_LEN,1,[Has sdl_len in sockaddr_dl]),,[
 #include <sys/types.h>
 #include <sys/socket.h>
 #include <net/if_dl.h>
		])
fi

# Checking for the right bpf header to include
AC_CHECK_HEADER(pcap-bpf.h)
AC_CHECK_HEADER(net/bpf.h)

# Checking for libgdc
AC_CHECK_HEADER(gdc.h,AC_DEFINE(HAVE_LIBGDC,1,[has gdc.h]))

# Check for libwandio (no longer bundled with libtrace)
AC_CHECK_LIB(wandio,wandio_wflush,wandiofound=1,wandiofound=0)
if test "$wandiofound" = 0; then
	AC_MSG_ERROR(libwandio 4.0.0 or better is required to compile this version of libtrace. If you have installed libwandio in a non-standard location please use LDFLAGS to specify the location of the library. WANDIO can be obtained from https://github.com/LibtraceTeam/wandio)
else
	LIBTRACE_LIBS="$LIBTRACE_LIBS -lwandio"
        TOOLS_LIBS="$TOOLS_LIBS -lwandio"
	AC_DEFINE([HAVE_LIBWANDIO],1,[compile with libwandio support])
fi

AC_CHECK_LIB(crypto, EVP_EncryptInit_ex, cryptofound=1, cryptofound=0)
if test "$cryptofound" = 1; then
	AC_CHECK_HEADER(openssl/evp.h, cryptofound=1, cryptofound=0)
fi
AC_CHECK_LIB(yaml, yaml_parser_initialize, yamlfound=1, yamlfound=0)
AC_CHECK_LIB(tasn1, asn1_array2tree, asn1found=1, asn1found=0)

# pgo optimisations
pgo_generate=false
AC_ARG_ENABLE(pgo-generate, AS_HELP_STRING(--enable-pgo-generate, Generate performance guided optimization files),
[
	if test x"$enableval" = xyes ; then
		pgo_generate=true
		AC_CHECK_LIB(gcov, __gcov_init,,AC_MSG_ERROR([gcov is required for pgo optimizations]))
		CFLAGS+=" -fprofile-generate -fprofile-update=atomic -lgcov"
		CXXFLAGS+=" -fprofile-generate -fprofile-update=atomic -lgcov"
	fi
],[])
pgo_build=false
AC_ARG_ENABLE(pgo-build, AS_HELP_STRING(--enable-pgo-build, Build using performance guided optimization files),
[
	if test x"$enableval" = xyes ; then
		pgo_build=true
		AC_CHECK_LIB(gcov, __gcov_init,,AC_MSG_ERROR([gcov is required for pgo optimizations]))
		CFLAGS+=" -fprofile-correction -fprofile-use -lgcov"
		CXXFLAGS+=" -fprofile-correction -fprofile-use -lgcov"
	fi
],[])

systemtap=false
# check if the system has systemtap header
AC_ARG_ENABLE(dtrace, AS_HELP_STRING(--enable-dtrace, Include USDT tracepoints),
[
    if test "x$enableval" = xyes ; then
        AC_CHECK_HEADER(sys/sdt.h, [systemtap=true], [systemtap=false])
        if test "$systemtap" = false; then
            AC_MSG_ERROR([systemtap-sdt-dev is required to include USDT tracepoints])
        fi
        AC_DEFINE(ENABLE_DTRACE, 1, [Set to 1 if libtrace is to be compiled with USDT tracepoints])
    else
        AC_DEFINE(ENABLE_DTRACE, 0, [Set to 1 if libtrace is to be compiled with USDT tracepoints])
    fi
],
[
    AC_DEFINE(ENABLE_DTRACE, 0, [Set to 1 if libtrace is to be compiled with USDT tracepoints])
])

# check for xdp requirements.
libtrace_xdp=false
build_ebpf=false

AC_ARG_WITH(xdp, AS_HELP_STRING(--with-xdp, include XDP capture support),
[
    if test x"$withval" = xno; then
        want_xdp=no
    else
        want_xdp=yes
    fi
],[ want_xdp=ifpresent])

if test "$want_xdp" != no; then
    AC_CHECK_LIB(elf, elf_begin, elffound=1, elffound=0)

    if test "$elffound" = 1; then
        AC_CHECK_LIB(xdp, xsk_socket__create, xdpfound=1, xdpfound=0, -lelf)

        if test "$xdpfound" = 1; then
            # check for libbpf
            AC_CHECK_LIB(bpf, bpf_map_update_elem, bpffound=1, bpffound=0,
            -lelf -lxdp)

            if test "$bpffound" = 1; then
                AC_DEFINE(HAVE_LIBBPF, 1, [Set to 1 if libbpf is available])
                ADD_LDFLAGS="$ADD_LDFLAGS -lbpf -lelf -lxdp"
                libtrace_xdp=true

                AC_CHECK_LIB(bpf, bpf_xdp_query_id, bpfnewer=1, bpfnewer=0,
                        -lelf -lxdp)
                if test "$bpfnewer" = 1; then
                    AC_DEFINE(HAVE_LIBBPF_XDP_QUERYID, 1, [Set to 1 if libbpf supports bpf_xdp_query_id])
                fi

                # check for requirements to build XDP eBPF kernel
                AC_CHECK_PROG(CLANG, [clang], [clang], [no])
                if test "$CLANG" != "no"; then

                    AC_SUBST(CLANG)
                    llc_candidates=$($CLANG --version | \
                        awk '/^clang version/ {
                        split($3, v, ".");
                        printf("llc-%s.%s llc-%s llc", v[[1]], v[[2]], v[[1]])
                    }')
                    AC_CHECK_PROGS([LLC], [$llc_candidates], [no])
                    if test "$LLC" != "no"; then
                        AC_SUBST(LLC)
                        build_ebpf=true
                    else
                        llc_candidates=$($CLANG --version | \
                            awk '/ clang version/ {
                            split($4, v, ".");
                            printf("llc-%s.%s llc-%s llc", v[[1]], v[[2]], v[[1]])
                        }')
                        LLC=""
                        AC_CHECK_PROGS([LLC], [$llc_candidates], [no])
                        if test "$LLC" != "no"; then
                            AC_SUBST(LLC)
                            build_ebpf=true
                        fi
                    fi

                 fi
             else
                 libtrace_xdp=false
             fi
        else
            libtrace_xdp=false
        fi
    fi
fi

# was xdp build explicitly specified and failed
if test "$want_xdp" = yes -a "$libtrace_xdp" = false; then
    AC_MSG_ERROR([libelf, libxdp and libbpf are required for XDP support])
fi

# was ebpf build explicitly specified and failed
AC_ARG_ENABLE(ebpf-build, AS_HELP_STRING(--enable-ebpf-build, Enable compilation of XDP eBPF kernel),
[
    if test "x$enableval" = xyes ; then
        if test x"$want_xdp" != xyes -a x"$build_ebpf" = xfalse; then
            AC_MSG_ERROR([XDP required for ebpf build, set --with-xdp])
        fi
        if test x"$build_ebpf" = xfalse; then
            AC_MSG_ERROR([clang, llvm and libbpf are required to build the XDP eBPF kernel])
        fi
    fi
],[])


# Check for libpcap
AC_CHECK_LIB(pcap,pcap_next_ex,pcapfound=1,pcapfound=0)
AC_CHECK_LIB(pcap,pcap_create,pcapcreate=1,pcapcreate=0)
AC_CHECK_LIB(pcap,pcap_set_immediate_mode,pcapimmediate=1,pcapimmediate=0)
AC_CHECK_DECLS([BIOCSETIF],,,[
#include <sys/types.h>
#include <sys/time.h>
#include <sys/ioctl.h>
#include <net/bpf.h>
])

AC_ARG_ENABLE(memory-debugging,
                AS_HELP_STRING(--enable-memory-debugging, prints internal memory statistics),[
                if test "$HAVE_TLS" = 1 -a "x$enableval" = xyes
                then
                    AC_DEFINE([ENABLE_MEM_STATS], 1, [print debug memory statistics])
                fi
],[])

# Configure options for man pages
AC_ARG_WITH(man,
	    AS_HELP_STRING(--with-man,install man pages by default),[
	     if test "$withval" = yes
	     then
		     MANPAGES="docs/man/man3/[a-zA-Z]*.3"
	     else
		     MANPAGES=""
	     fi
])

# Complain if we didn't find a suitable libpcap
if test "$pcapfound" = 0; then
	AC_MSG_ERROR(libpcap0.8 or greater is required to compile libtrace. If you have installed it in a non-standard location please use LDFLAGS to specify the location of the library)
else
	TOOLS_LIBS="$TOOLS_LIBS -lpcap"
	LIBTRACE_LIBS="$LIBTRACE_LIBS -lpcap"
	AC_DEFINE([HAVE_LIBPCAP],1,[compile with libpcap support])

	LIBS="-lpcap"
	AC_CHECK_FUNCS(pcap_inject pcap_sendpacket pcap_setnonblock)
	LIBS=""

fi

if test "$pcapcreate" = 1; then
	AC_DEFINE([HAVE_PCAP_CREATE],1,[compile with libpcap 1.0 support])
fi

if test "$pcapimmediate" = 1; then
        AC_DEFINE([HAVE_PCAP_IMMEDIATE],1,[able to use pcap_set_immediate_mode])
fi        

# Configure options for use of DAG cards
# Originally borrowed from libpcap, but extended quite a bit :)
# More details on how this check works: 
#	https://github.com/LibtraceTeam/libtrace/wiki

AC_ARG_WITH(dag, 
	    AS_HELP_STRING(--with-dag[=DIR],include DAG live capture support (located in directory DIR, if supplied)),
[
        if test "$withval" = no
        then
                want_dag=no
        elif test "$withval" = yes
        then
                want_dag=yes
                dag_root=/usr/local/dag
        else
                want_dag=yes
                dag_root=$withval
        fi
],[
        #
        # Use DAG API if present, otherwise don't
        #
        want_dag=ifpresent
        dag_root=/usr/local/dag
])

# DAG 3.0 actually puts header files and shared libraries into sensible
# places now, so we should be able to do a simple CHECK_LIB to see if
# they're there!

# Addendum: It turns out DAG 2.5 does this too, so we'll match DAG2.5 in here
# also. This isn't such a bad thing, the DAG2.5 API is essentially the same as
# DAG 3.0 and libtrace will use the same format_dagXX source for both
libtrace_dag=false
libtrace_dag_version=none

if test "$with_dag" != no; then
	AC_CHECK_LIB(dag, dag_open, dag_found=1, dag_found=0)
	if test "$dag_found" = 1; then
		ADD_LIBS="$ADD_LIBS -ldag"
		LIBTRACE_LIBS="$LIBTRACE_LIBS -ldag"
		AC_DEFINE(HAVE_DAG_API, 1, [define if you have the DAG API])
		AC_DEFINE(HAVE_DAG,1,[conditional for building with DAG live capture support])
		libtrace_dag=true
		
		AC_DEFINE(DAG_VERSION,25,[defines the DAG driver version])
		libtrace_dag_version=25

		AC_CHECK_HEADERS(dag_config_api.h,[csapi_found=1],,)
		if test "$csapi_found" = 1; then
			LIBTRACE_LIBS="$LIBTRACE_LIBS -ldagconf"
		fi
	fi	
fi

# TODO: turn the test part of the next block into a function, so it can be 
# called multiple times for $dag_root, /root/dag, /usr/local/lib
dag_drv_v="DAG not present"

# Now check for 2.4 DAG drivers which don't install as cleanly!
if test "$with_dag" != no -a "$libtrace_dag" = false; then
        AC_MSG_CHECKING(whether we have DAG 2.4 API instead)

	if test -r "$dag_root/lib"; then
		dag_lib_dir="$dag_root/lib"
	else
		dag_lib_dir="$dag_root"
	fi
	
	if test -r "$dag_root/include"; then
		dag_tools_dir="$dag_root/tools"
                dag_include_dir="$dag_root/include"
        else
                dag_tools_dir="$dag_root"
                dag_include_dir="$dag_root"
	fi

        if test -r "$dag_include_dir/dagapi.h" -a -r "$dag_lib_dir/libdag.a"; 
	then
                ADD_INCLS="$ADD_INCLS -I $dag_include_dir"
		ADD_LIBS="$ADD_LIBS -ldag"
		ADD_LDFLAGS="$ADD_LDFLAGS -L$dag_lib_dir"
		DAG_TOOLS_DIR=$dag_tools_dir
		AC_SUBST([DAG_TOOLS_DIR])
                libtrace_dag=true
                
		AC_DEFINE(HAVE_DAG_API, 1, [define if you have the DAG API])
		AC_DEFINE(HAVE_DAG,1,[conditional for building with DAG live capture support])
		AC_DEFINE(DAG_VERSION, 24, [defines the DAG driver version])
		libtrace_dag_version=24
        else
                if test "$want_dag" = yes; then
                        AC_MSG_ERROR(DAG API not found under directory $dag_root
; use --without-dag)
                fi
                libtrace_dag=false
        fi
        AC_MSG_RESULT($libtrace_dag)

fi

# Check for PACKET_FANOUT (borrowed from Suricata)
AC_CHECK_DECL([PACKET_FANOUT],
        AC_DEFINE([HAVE_PACKET_FANOUT],[1],
        [Recent packet fanout support is available]),
        [],
        [[#include <linux/if_packet.h>]])

# If we use DPDK we might be able to use libnuma
AC_ARG_WITH(numa,
	AS_HELP_STRING(--without-numa,disable NUMA support),
[
	if test "$withval" = no
	then
		want_numa=no
	else
		want_numa=yes
	fi
],[
	# Default to building with NUMA
	want_numa=yes
])

if test "$want_numa" != no; then
	AC_CHECK_LIB(numa, numa_node_to_cpus, have_numa=1, have_numa=0)
fi

# Need libwandder for ETSI live decoding
AC_CHECK_LIB(wandder, wandder_etsili_get_cc_format, have_wandder=1, have_wandder=0)

# Checks for various "optional" libraries
AC_CHECK_LIB(pthread, pthread_create, have_pthread=1, have_pthread=0)

AC_CHECK_LIB(pthread, pthread_setname_np, have_pthread_setname_np=1, have_pthread_setname_np=0)



# Try to determine the DAG driver version
#if test x"$libtrace_dag" = xtrue; then
#       dag_drv_v="Unknown"
#	files=`locate /usr/*dag*/VERSION`
#       file_count=0
#
#       for i in $files; do
#                if $file_count > 0; then
#                        dag_drv_v="Indeterminate"
#                        break
#                fi
#                dag_drv_v=`cat $i`
#                file_count=$file_count+1
#        done
#fi
#DAG_VERSION_NUM=$dag_drv_v

# Check for PF_RING 
AC_ARG_WITH(pfring,
	    AS_HELP_STRING(--with-pfring,include PF_RING live capture support (if PF_RING libraries are available)),
[
        if test "$withval" = no
        then
            want_pfring=no
        else
            want_pfring=ifpresent
        fi
],[
        want_pfring=ifpresent
])


libtrace_pfring=false
if test "$want_pfring" != no; then
	# bit annoying that we have to include extra libs to account for
	# optional pfring modules that depend on these libs
	AC_SEARCH_LIBS(pfring_recv, pfring, pfring_found=1, pfring_found=0,[-lpcap])
	if test "$pfring_found" = 1; then
		ADD_LIBS="$ADD_LIBS -lpfring"
		LIBTRACE_LIBS="$LIBTRACE_LIBS -lpfring"
		AC_DEFINE(HAVE_PFRING,1,[conditional for building with DAG live capture support])
		libtrace_pfring=true
	fi	
fi

# Check for ncurses

# These libraries have to be explicitly linked in OpenSolaris
AC_SEARCH_LIBS(getservent, socket, have_socket=1, have_socket=0, -lnsl)
LIBS=

AC_SEARCH_LIBS(inet_ntop, nsl, have_nsl=1, have_nsl=0, -lsocket)
LIBS=

# Check to see if we have libdl - *BSD has built-in libdl
AC_SEARCH_LIBS(dlopen,dl, dlfound=1,dlfound=0 )
LIBS=

# Check for clock_gettime
AC_CHECK_LIB(rt, clock_gettime, have_clock_gettime=1, have_clock_gettime=0)
LIBS=

if test "$have_numa" = 1; then
	LIBTRACE_LIBS="$LIBTRACE_LIBS -lnuma"
	AC_DEFINE(HAVE_LIBNUMA, 1, [Set to 1 if libnuma is supported])
	with_numa=yes
else
	with_numa=no
fi

if test "$have_wandder" = 1; then
        LIBTRACE_LIBS="$LIBTRACE_LIBS -lwandder"
        AC_DEFINE(HAVE_WANDDER, 1, [Set to 1 if libwandder is available])
        wandder_avail=yes
else
        wandder_avail=no
fi

if test "$dlfound" = 0; then
	AC_MSG_ERROR("Unable to find dlopen. Please use LDFLAGS to specify the location of libdl and re-run configure")
fi

if test "$ac_cv_search_dlopen" != "none required"; then 
	LIBPKTDUMP_LIBS="$LIBPKTDUMP_LIBS $ac_cv_search_dlopen"
	if test "$dpdk_found" != 0; then
		LIBTRACE_LIBS="$LIBTRACE_LIBS -Wl,$ac_cv_search_dlopen"
	fi
fi

if test "$have_pthread" = 1; then
	AC_DEFINE(HAVE_LIBPTHREAD, 1, [Set to 1 if pthreads are supported])
        LIBTRACE_LIBS="$LIBTRACE_LIBS -lpthread"
fi

if test "$have_pthread_setname_np" = 1; then
	AC_DEFINE(HAVE_PTHREAD_SETNAME_NP, 1, [Set to 1 if pthread_setname_np is found])
fi

if test "$cryptofound" = 1; then
        AC_DEFINE(HAVE_LIBCRYPTO, 1, [Set to 1 if libcrypto is available])
        TOOLS_LIBS="$TOOLS_LIBS -lcrypto"
        have_crypto=yes
else
        have_crypto=no
fi

if test "$yamlfound" = 1; then
        AC_DEFINE(HAVE_LIBYAML, 1, [Set to 1 if libyaml is available])
        TOOLS_LIBS="$TOOLS_LIBS -lyaml"
        have_yaml=yes
else
        have_yaml=no
fi

if test "$have_nsl" = 1; then
	if test "$ac_cv_search_inet_ntop" != "none required"; then
		LIBTRACE_LIBS="$LIBTRACE_LIBS $ac_cv_search_inet_ntop"
		LIBPKTDUMP_LIBS="$LIBPKTDUMP_LIBS $ac_cv_search_inet_ntop"
	fi
fi

if test "$have_socket" = 1; then
	if test "$ac_cv_search_getservent" != "none required"; then
		LIBTRACE_LIBS="$LIBTRACE_LIBS $ac_cv_search_getservent"
		LIBPKTDUMP_LIBS="$LIBPKTDUMP_LIBS $ac_cv_search_getservent"
	fi 
fi


if test "$have_clock_gettime" = 1; then
	LIBTRACE_LIBS="$LIBTRACE_LIBS -lrt"
	AC_DEFINE(HAVE_CLOCK_GETTIME, 1, [Set to 1 if clock_gettime is supported])
	with_clock_gettime=yes
else
	with_clock_gettime=no
fi

# Check for DPDK 
AC_ARG_WITH(dpdk,
	    AS_HELP_STRING(--with-dpdk,include DPDK live capture support (From either the RTE_SDK/_TARGET environment variable or the dpdk-dev package)),
[
        if test "$withval" = no
        then
            want_dpdk=no
        else
            want_dpdk=ifpresent
        fi
],[
        # Default to building without DPDK format
        want_dpdk=no
])

libtrace_dpdk=false
if test "$want_dpdk" != no; then
	dpdk_found=0
	# So instead simply check for existence
	if test "$dpdk_found" = 0 -a "$RTE_SDK" != ""; then
		AC_CHECK_FILE("$RTE_SDK/$RTE_TARGET/lib/libdpdk.so", dpdk_found="dpdk", dpdk_found=0)
		AC_SUBST(DPDK_STATIC, 0)
	fi
	if test "$dpdk_found" = 0 -a "$RTE_SDK" != ""; then
		AC_CHECK_FILE("$RTE_SDK/$RTE_TARGET/lib/libintel_dpdk.a", dpdk_found=":libintel_dpdk.a", dpdk_found=0)
		AC_SUBST(DPDK_STATIC, 1)
	fi
	# DPDK 2.1.0+ renames this to libdpdk from libintel_dpdk
	if test "$dpdk_found" = 0 -a "$RTE_SDK" != ""; then
		AC_CHECK_FILE("$RTE_SDK/$RTE_TARGET/lib/libdpdk.a", dpdk_found=":libdpdk.a", dpdk_found=0)
		AC_SUBST(DPDK_STATIC, 1)
	fi
	if test "$dpdk_found" = 0 -a "$RTE_SDK" != ""; then
		AC_CHECK_LIB(dpdk, rte_eth_dev_configure, dpdk_found="dpdk", dpdk_found=0)
		AC_SUBST(DPDK_STATIC, 0)
	fi
	if test "$dpdk_found" != 0 -a "$RTE_SDK" != ""; then
		# Save these now so that they can be re-exported later
		AC_SUBST([RTE_TARGET])
		AC_SUBST([RTE_SDK])
		# Force dpdk library to be statically linked to allow compiler optimisations
		LIBTRACE_LIBS="-Wl,-z,defs $LIBTRACE_LIBS -Wl,--whole-archive -Wl,-l$dpdk_found -Wl,--no-whole-archive"
		dpdk_path="$RTE_SDK"
	fi

        # Look for a DPDK meson build at $RTE_SDK/install or $RTE_SDK/
        # or $RTE_SDK/libdpdk.pc using pkg-config
        if test "$dpdk_found" = 0 -a "$RTE_SDK" != "" -a -d "$RTE_SDK"; then
                AC_MSG_NOTICE(Trying to find a meson DPDK build at $RTE_SDK)

                if test -f "$RTE_SDK/install/lib/x86_64-linux-gnu/pkgconfig/libdpdk.pc"; then
                        export PKG_CONFIG_PATH="$RTE_SDK/install/lib/x86_64-linux-gnu/pkgconfig"
                elif test -f "$RTE_SDK/lib/x86_64-linux-gnu/pkgconfig/libdpdk.pc"; then
                        export PKG_CONFIG_PATH="$RTE_SDK/lib/x86_64-linux-gnu/pkgconfig"
                elif test -f "$RTE_SDK/libdpdk.pc"; then
                        export PKG_CONFIG_PATH="$RTE_SDK"
                fi
                if test -n "$PKG_CONFIG_PATH" ; then
                        PKG_PROG_PKG_CONFIG
                        pkg_config_old="$PKG_CONFIG"
                        PKG_CONFIG="$PKG_CONFIG --static"
                        PKG_CHECK_MODULES([DPDK], [libdpdk >= 18], [pkgconf_dpdk_found="yes"],
                                          [pkgconf_dpdk_found="no"])
                        PKG_CONFIG="$pkg_config_old"
                        # Add library directory as RUNPATH in library (-R option)
                        # Used to load the correct DPDK version when not statically linked
                        DPDK_LIBS="-Wl,-z,defs $(echo $DPDK_LIBS | sed -E 's!-L(/[[^ ]]*)!-R\1 -L\1!g')"
                        unset PKG_CONFIG_PATH
                        AC_SUBST(DPDK_STATIC, 1)
                fi
        fi

	# Find the DPDK system package
	# We do this last to allow RTE_SDK to override the system library
	#
	# Dynamic linking against the system's version of DPDK is never easy:
	#
	# 1) On Ubuntu the linker has --as-needed set by default, this means
	# any library which libtrace doesn't directly reference a method from
	# is not listed as a dependency to libtrace.so. This causes problems
	# for DPDK, DPDK uses .init or .ctors to patch drivers etc into its
	# global structures. So libraries need to be loaded simply to register
	# themselves.
	#
	# 2) The answer to this is --no-as-needed, a positional argument, which
	# applies to all following arguments/libs. However, libtool *helpfully*
	# reorganises the arguments we give it before giving them to the linker.
	# To avoid this we need to prefix all libraries with -Wl,-lxxx, so
	# libtool leaves them be.
	# Hence the ugly sed hack, TODO find something nicer.
	#
	#
        if test "$dpdk_found" = 0 -a n"$RTE_SDK" = n""; then
                PKG_PROG_PKG_CONFIG
                PKG_CHECK_MODULES([DPDK], [libdpdk >= 18], [pkgconf_dpdk_found="yes"],
                                  [pkgconf_dpdk_found="no"])
                AC_SUBST(DPDK_STATIC, 0)
        fi
        # DPDK has been using pkgconf (user-defined or system path)
        if test "$dpdk_found" = 0 -a "x$pkgconf_dpdk_found" = "xyes"; then

                #!!Disabled because DPDK still tries to load plugins twice!!
                # Try and statically link DPDK into libtrace
                # - Prior to DPDK 20.08 requires the -Bstatic flag
                #   - But, plugins (pmd drivers) get loaded twice and error
                # - 20.08 and newer instead provides -l:librte_xx.a
                #   - This works correctly as is
                #case "$DPDK_LIBS" in
                #    *"-l:"*)
                #        ;;
                #    *)
                #        DPDK_LIBS="-Wl,-Bstatic $DPDK_LIBS -Wl,-Bdynamic"
                #esac

                # Change -l to -Wl,-l to stop libtool reordering flags like --no-as-needed
                DPDK_LIBS="-Wl,--no-as-needed $(echo $DPDK_LIBS | sed 's/ -l/ -Wl,-l/g')"
                # Warn about undefined references (-Wl,-z,defs)
                # Add libc and libm to the end of the arguments again
                # because libtool reorganises library order
                DPDK_LIBS="-Wl,-z,defs $DPDK_LIBS -Wl,-lm -Wl,-lc"
                LIBTRACE_LIBS="$LIBTRACE_LIBS $DPDK_LIBS"
                ADD_INCLS="$ADD_INCLS $DPDK_CFLAGS"
                AC_MSG_NOTICE([Building against DPDK using pkg-config])
                dpdk_found=pkgconfig
                dpdk_path="$(echo $DPDK_LIBS | sed -nE 's/.*-L([[^ ]]*).*/\1/gp')"
                if test -z "$dpdk_path" ; then
                        dpdk_path=system
                fi
        fi

	if test "$dpdk_found" = 0 -a "$RTE_SDK" = ""; then
		AC_MSG_NOTICE([No RTE_SDK given, checking for system dpdk-dev package])
		# Search the system, maybe it is installed? Ethdev is one of the main libraries
		# On Ubuntu and Debian we can simply check for dpdk.so
		AC_CHECK_LIB(dpdk, rte_eth_dev_configure, dpdk_found="system", dpdk_found=0)
		# We also need to check that rte.vars.mk is installed from dpdk-dev (as well as libdpdk-dev)
		if test "$dpdk_found" != 0 -a -e /usr/share/dpdk/mk/rte.vars.mk ; then
                        # This is terrible, but I'm not sure that there is an easier way for us to figure out what RTE_EXEC_ENV is eventually going to be renamed to by the end of the DPDK build process.
                        for execenv in linuxapp linux bsdapp freebsd; do
                                if test -d /usr/share/dpdk/$(uname -m)-default-${execenv}-gcc; then
                                        RTE_TARGET=$(uname -m)-default-${execenv}-gcc
                                        break
                                fi
                        done

                        if test RTE_TARGET != ""; then
				RTE_SDK="/usr/share/dpdk/"
				RTE_INCLUDE="/usr/include/dpdk"
				AC_SUBST([RTE_TARGET])
				AC_SUBST([RTE_SDK])
				AC_SUBST([RTE_INCLUDE])
				LIBTRACE_LIBS="$LIBTRACE_LIBS -ldpdk"
				AC_MSG_NOTICE([Building against system DPDK])

				dpdk_found=system
				dpdk_path=system
				AC_SUBST(DPDK_STATIC, 0)
			fi
		fi
	fi

        if test "$dpdk_found" != 0; then
		AC_DEFINE(HAVE_DPDK,1,[conditional for building with DPDK live capture support])
                libtrace_dpdk=true
        fi


        if test "$dpdk_found" = 0 -a "$with_dpdk" = "yes"; then
                AC_MSG_ERROR("Unable to find DPDK on system. Build halted -- consider re-running configure with '--with-dpdk=no'.")
        fi
fi


have_memfd_create=no
AC_RUN_IFELSE([AC_LANG_PROGRAM([
#include <sys/syscall.h>
],[
#ifndef __NR_memfd_create
exit(1)
#endif
])], [have_memfd_create=yes])

if test "$have_memfd_create" = "yes"; then
        AC_DEFINE(HAVE_MEMFD_CREATE, 1, [Set to 1 if kernel supports memfd_create syscall])
fi

# Stupid AC_SEARCH_LIB appends stuff to LIBS even when I don't want it to, so
# just set libs to null here to avoid linking against them by default
LIBS=

# For now, the user has to explicitly ask for the LLVM stuff, as it's a bit
# rough around the edges :(
AC_ARG_WITH([llvm],
	[AS_HELP_STRING([--with-llvm],[support Just In Time compiler])],
	use_llvm="yes",
	use_llvm="no")
JIT=no

# Search for LLVM and the libraries that it requires
if (test "$use_llvm" != "no"); then
	AC_PATH_PROG(LLVM_CONFIG, llvm-config, no)

	# XXX Hard coding the path is REALLY bad.
	# llvm-gcc is installed in a stupid place in Debian / Ubuntu. Hard luck
	# if you've installed it somewhere custom.
	# Hopefully in future we can replace this with clang, which may or may
	# not be installed somewhere intelligent.
	AC_PATH_PROG(LLVM_GCC, llvm-gcc, no, /usr/lib/llvm/llvm/gcc-4.2/bin)

	AC_LANG_PUSH([C++])
	AC_CHECK_HEADERS([boost/lexical_cast.hpp], boost="yes", boost="no")
	AC_LANG_POP([C++])

		
	if test "$boost" = "no"; then
		AC_MSG_NOTICE([Unabled to find boost libraries.  JIT support disabled.])
		JIT=no
	elif test "$LLVM_CONFIG" = "no" ; then
		AC_MSG_NOTICE([Unable to find llvm-config.  JIT support disabled.])
		JIT=no
	elif test "$LLVM_GCC" = "no"; then
		AC_MSG_NOTICE([Unable to find llvm-gcc.  JIT support disabled.])
		JIT=no
	else
		LIBCXXFLAGS="`$LLVM_CONFIG --cxxflags` $CXXFLAGS"
		LIBTRACE_LIBS="$LIBTRACE_LIBS `$LLVM_CONFIG --libs all`";
		LDFLAGS="`$LLVM_CONFIG --ldflags` $LDFLAGS";
		JIT=yes
		AC_DEFINE(HAVE_LLVM, 1, [Set to 1 if you have LLVM installed])
	fi
fi

PKG_CHECK_MODULES(ncurses,ncurses,have_ncurses=yes,have_ncurses=no)

AC_ARG_WITH([ncurses],
	AS_HELP_STRING([--with-ncurses],[build tracetop (requires ncurses)]))

AS_IF([test "x$with_ncurses" != "xno"],
	[AC_SEARCH_LIBS(mvprintw, ncurses, have_ncurses=yes, have_ncurses=no)
	 LIBS=
	], [have_ncurses=no])

AS_IF([test "x$have_ncurses" = "xyes"], [
	if test "$ac_cv_search_mvprintw" != "none required"; then
		TOOLS_LIBS="$TOOLS_LIBS $ac_cv_search_mvprintw"
	fi
	AC_DEFINE(HAVE_NCURSES, 1, "Compiled with ncurses support")
	with_ncurses=yes],

	
	[AS_IF([test "x$with_ncurses" = "xyes"],
		[AC_MSG_ERROR([ncurses requested but not found])])
	AC_DEFINE(HAVE_NCURSES, 0, "Compiled with ncurses support")
	with_ncurses=no]
)

# Define automake conditionals for use in our Makefile.am files
AM_CONDITIONAL([HAVE_BPF_CAPTURE], [test "$ac_cv_have_decl_BIOCSETIF" = yes ])
AM_CONDITIONAL([HAVE_DAG], [test "$libtrace_dag" = true])
AM_CONDITIONAL([HAVE_PFRING], [test "$libtrace_pfring" = true])
AM_CONDITIONAL([HAVE_DPDK], [test "$libtrace_dpdk" = true])
AM_CONDITIONAL([HAVE_WANDDER], [test "x$wandder_avail" = "xyes"])
AM_CONDITIONAL([DAG2_4], [test "$libtrace_dag_version" = 24])
AM_CONDITIONAL([DAG2_5], [test "$libtrace_dag_version" = 25])
AM_CONDITIONAL([HAVE_NETPACKET_PACKET_H], [test "$libtrace_netpacket_packet_h" = true])
AM_CONDITIONAL([HAVE_LIBGDC], [test "$ac_cv_header_gdc_h" = yes])
AM_CONDITIONAL([HAVE_LLVM], [test "x$JIT" != "xno" ])
AM_CONDITIONAL([HAVE_NCURSES], [test "x$with_ncurses" != "xno"])
AM_CONDITIONAL([HAVE_YAML], [test "x$have_yaml" != "xno"])

AM_CONDITIONAL([HAVE_DPDK_PKGCONFIG], [test "x$dpdk_found" = "xpkgconfig"])

AM_CONDITIONAL([HAVE_LIBBPF], [test "x$libtrace_xdp" = "xtrue"])
AM_CONDITIONAL([BUILD_EBPF], [test "x$build_ebpf" = "xtrue"])

AM_CONDITIONAL([ENABLE_DTRACE], [test "x$systemtap" = "xtrue"])
AM_CONDITIONAL([HAVE_LINUX_ETHTOOL_H], [test "x$found_ethtool" = "xtrue"])

# Check for miscellaneous programs
AC_CHECK_PROG([libtrace_doxygen], [doxygen], [true], [false])

AM_CONDITIONAL(HAS_DOXYGEN, [test x"$libtrace_doxygen" = xtrue])

# Set all our output variables
AC_SUBST([LIBTRACE_LIBS])
AC_SUBST([LIBPKTDUMP_LIBS])
AC_SUBST([TOOLS_LIBS])
#AC_SUBST([ADD_LIBS])
AC_SUBST([ADD_LDFLAGS])
AC_SUBST([ADD_INCLS])
AC_SUBST([LTLIBOBJS])
AC_SUBST([MANPAGES])
AC_SUBST([PACKAGE_VERSION])
AC_SUBST([LIBTRACE_MAJOR])
AC_SUBST([LIBTRACE_MID])
AC_SUBST([LIBTRACE_MINOR])
AC_SUBST([DAG_VERSION_NUM])
AC_SUBST([HAVE_BPF_CAPTURE])
AC_SUBST([HAVE_LIBGDC])
AC_SUBST([HAVE_LLVM])
AC_SUBST([HAVE_NCURSES])
AC_SUBST([LIBCFLAGS])
AC_SUBST([LIBCXXFLAGS])


# Finally, output our Makefiles
AC_OUTPUT

# Function for reporting whether an option was set or not
reportopt() {
	if test x"$2" = xtrue -o x"$2" = xyes; then
		AC_MSG_NOTICE([$1: Yes])
	else 
		AC_MSG_NOTICE([$1: No])
	fi
}


# Report which configure options were set
echo
AC_MSG_NOTICE([Libtrace version $PACKAGE_VERSION])
if test "$pcapcreate" = 1; then
	AC_MSG_NOTICE([Compiled with PCAP support: >= 1.0])
else
	AC_MSG_NOTICE([Compiled with PCAP support: < 1.0])
fi

if test x"$libtrace_dag" = xtrue; then
	if test "$libtrace_dag_version" = 24; then
		AC_MSG_NOTICE([Compiled with DAG live capture support: 2.4])
	elif test "$libtrace_dag_version" = 30; then
		AC_MSG_NOTICE([Compiled with DAG live capture support: 3.0])
	else
		AC_MSG_NOTICE([Compiled with DAG live capture support: 2.5])
	fi	
else
	AC_MSG_NOTICE([Compiled with DAG live capture support: No])
fi

# Performance guided optimization
if test x"$pgo_generate" = xtrue; then
	AC_MSG_NOTICE([Generate performance optimization files: Yes])
fi
if test x"$pgo_build" = xtrue; then
	AC_MSG_NOTICE([Compile with performance guided optimization files: Yes])
fi

# Tracepoints
if test x"$systemtap" = xtrue; then
	AC_MSG_NOTICE([Compiled with USDT tracepoints: Yes])
else
	AC_MSG_NOTICE([Compiled with USDT tracepoints: No])
fi

# Are we building with XDP support
if test x"$libtrace_xdp" = xtrue; then
	AC_MSG_NOTICE([Compiled with XDP capture support: Yes])
else
	AC_MSG_NOTICE([Compiled with XDP capture support: No])
fi

# Are we building eBPF XDP program
if test x"$build_ebpf" =  xtrue; then
    AC_MSG_NOTICE([XDP Libtrace BPF program will be compiled: Yes])
else
    AC_MSG_NOTICE([XDP Libtrace BPF program will be compiled: No])
fi

if test x"$libtrace_dpdk" = xtrue; then
	AC_MSG_NOTICE([Compiled with DPDK live capture support: Yes])
	AC_MSG_NOTICE([	DPDK path: $dpdk_path])
	reportopt "Compiled with DPDK trace NUMA support" $with_numa
	reportopt "Compiled with clock_gettime support" $with_clock_gettime
elif test x"$want_dpdk" != "xno"; then
#   We don't officially support DPDK so only report failure if the user
#   explicitly asked for DPDK. That way, we can hopefully keep it hidden
#   from most users for now...

	AC_MSG_NOTICE([Compiled with DPDK live capture support: No])
	AC_MSG_NOTICE([Note: Requires DPDK v1.5 or newer])
fi

if test x"$libtrace_pfring" = xtrue; then
	AC_MSG_NOTICE([Compiled with PF_RING live capture support: Yes])
else
	AC_MSG_NOTICE([Compiled with PF_RING live capture support: No])
fi

reportopt "Compiled with LLVM BPF JIT support" $JIT
reportopt "Compiled with live ETSI LI support (requires libwandder >= 2.0.6)" $wandder_avail
reportopt "Building man pages/documentation" $libtrace_doxygen
reportopt "Building tracetop (requires libncurses)" $with_ncurses
reportopt "Building traceanon (requires libyaml)" $have_yaml

if test x"$have_yaml" = xyes; then
    reportopt "Building traceanon with CryptoPan (requires libcrypto and openssl/evp.h)" $have_crypto
fi


# Report any errors relating to missing bison, flex, etc.
echo 
ac_cv_errcount=0;
if test -z "$YACC"; then
	AC_MSG_WARN(bison or yacc not found. Please install bison before continuing)
	ac_cv_errcount=$((ac_cv_errcount + 1))
fi

if test "x$LEX" != xflex -a "x$LEX" != xlex; then
	AC_MSG_WARN(flex or lex not found. Please install flex before continuing)
	ac_cv_errcount=$((ac_cv_errcount + 1))
fi

if test $ac_cv_errcount -gt 0; then
	AC_MSG_ERROR(Critical packages are missing and compilation will fail. Please install the packages listed above and rerun ./configure)
fi

